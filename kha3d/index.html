<!doctype html>
<html>
<head>
  <meta charset="utf-8">
  <meta name="viewport" content="initial-scale=1.0">
  <title>Kha3D</title>
  <link rel="stylesheet" href="css/standardize.css">
  <link rel="stylesheet" href="css/index.css">
  <script type="text/javascript" src="http://use.typekit.net/vsp8met.js"></script>
  <script type="text/javascript">try{Typekit.load();}catch(e){}</script>

  <link rel="stylesheet" href="http://luboslenco.com/hljs/styles/github.css">
  <script src="http://luboslenco.com/hljs/highlight.pack.js"></script>
  <script>hljs.initHighlightingOnLoad();</script>
</head>
<body>

  <a class="home" href="http://luboslenco.com"><img src="images/l-icon.png" class="logo"></a>
  <a href="#" class="hamburger"><img src="images/hamburger.png"></a>

  <nav class="primary-nav">
  <a href="#home">Home</a>
  <a href="#opening-a-window">1.Opening a window</a>
  <a href="#the-first-triangle">2.The first triangle</a>
  <a href="#matrices">3.Matrices</a>
  <a href="#a-colored-cube">4.A colored cube</a>
  <a href="#a-textured-cube">5.A textured cube</a>
  <a href="#keyboard-and-mouse">6.Keyboard and mouse</a>
  <a href="#model-loading">7.Model loading</a>
	<a href="#basic-shading">8.Basic shading</a>
  </nav>

  <article class="content">

	<section id="home">
	  <h1>Home</h1>
		<p>Following is a set of examples showing how to use modern, cross-platform 3D graphics API provided by <a href="https://github.com/KTXSoftware/Kha">Kha</a>.</p>
    <p>It is meant to be useful if you want to program truly portable 3D graphics in <a href="http://haxe.org">Haxe</a>, and also as a perfect starting point for learning 3D programming from scratch with a super clean API.</p>
    <p>If you have never heard of this Haxe Kha thing, know this - it allows you to do 3D graphics in the most portable and simplistic way, while still being as close to metal as possible. You can learn more <a href="http://tech.ktxsoftware.com/kha/">here</a>.</p>
    <p>We will be closely following excellent <a href="http://opengl-tutorial.org">opengl-tutorial.org</a> for theory parts, but a clean Kha implementation is built for every tutorial. Let's get started!</p>
	</section>

  <section id="opening-a-window">
    <h1>1.Opening a window</h1>

    <p>(<a href="http://www.opengl-tutorial.org/beginners-tutorials/tutorial-1-opening-a-window/">Reference</a>)</p>
    
    <p>The reference tutorial is not really needed here since Kha handles this stuff for us perfectly. Let's see how to setup Kha and get a blank screen to show up.</p>
    
    <p>To use Kha we need <a href="http://git-scm.com">git</a> and <a href="https://nodejs.org">node</a>(0.12.0+). Afterwards, open a terminal/command line (preferably in empty directory to keep things clean) and simply do 'git clone --recursive https://github.com/KTXSoftware/Empty' to get empty project.</p>

    <p>Go to 'Sources' directory and open 'Empty.hx'. We will add a rendering function that just clears the screen.</p>
    <p><pre><code class="hx">
package;

import kha.Game;
import kha.Framebuffer;
import kha.Color;

class Empty extends Game {

    public function new() {
        super("Empty");
    }

    override public function render(frame:Framebuffer) {
        // A graphics object which lets us perform 3D operations
        var g = frame.g4;

        // Begin rendering
        g.begin();

        // Clear screen to black
        g.clear(Color.Black);

        // End rendering
        g.end();
    }
}
    </code></pre></p>

    <p>You can access complete sources <a href="https://github.com/luboslenco/kha3d_examples">here</a>.</p>
    <p>Now on to building our project. Open terminal/command line in project folder(where project.kha resides). With Kha you can target pretty much every platform out there, but for simplicity let's do HTML5 build. Type 'node Kha/make -t html5' to build and 'node Kha/make --server' to start local server. Open '127.0.0.1:8080' in your browser and black screen will <a href="http://luboslenco.com/kha3d/build/01">show up</a>!</p>
    </section>

  <section id="the-first-triangle">
    <h1>2.The first triangle</h1>
    
    <p>(<a href="http://www.opengl-tutorial.org/beginners-tutorials/tutorial-2-the-first-triangle/">Reference</a>)</p>
    
    <p>Here comes the most exciting moment of your life - drawing a triangle! The first thing we need to do is define triangle data and a shader program.</p>

    <p><pre><code class="hx">
// An array of 3 vectors representing 3 vertices to form a triangle
static var vertices:Array&lt;Float&gt; = [
   -1.0, -1.0, 0.0, // Bottom-left
    1.0, -1.0, 0.0, // Bottom-right
    0.0,  1.0, 0.0  // Top
];
// Indices for our triangle, these will point to vertices above
static var indices:Array&lt;Int&gt; = [
  0, // Bottom-left
  1, // Bottom-right
  2  // Top
];

var vertexBuffer:VertexBuffer;
var indexBuffer:IndexBuffer;
var program:Program;
    </code></pre></p>

    <p>Proceed to load shaders and link shader program in init() function.</p>

    <p><pre><code class="hx">
// Define vertex structure
var structure = new VertexStructure();
structure.add("pos", VertexData.Float3);

// Save length - we only store position in vertices for now
// Eventually there will be texture coords, normals,...
var structureLength = 3;

// Load shaders - these are located in 'Sources/Shaders' directory
// and Kha includes them automatically
var fragmentShader = new FragmentShader(Loader.the.getShader("simple.frag"));
var vertexShader = new VertexShader(Loader.the.getShader("simple.vert"));

// Link program with fragment and vertex shaders we loaded
program = new Program();
program.setFragmentShader(fragmentShader);
program.setVertexShader(vertexShader);
program.link(structure);
    </code></pre></p>

    <p>Create vertex and index buffers from triangle data we defined.</p>

    <p><pre><code class="hx">
// Create vertex buffer
vertexBuffer = new VertexBuffer(
  Std.int(vertices.length / 3), // Vertex count - 3 floats per vertex
  structure, // Vertex structure
  Usage.StaticUsage // Vertex data will stay the same
);

// Copy vertices to vertex buffer
var vbData = vertexBuffer.lock();
for (i in 0...vbData.length) {
  vbData[i] = vertices[i];
}
vertexBuffer.unlock();

// Create index buffer
indexBuffer = new IndexBuffer(
  indices.length, // 3 indices for our triangle
  Usage.StaticUsage // Index data will stay the same
);

// Copy indices to index buffer
var iData = indexBuffer.lock();
for (i in 0...iData.length) {
  iData[i] = indices[i];
}
indexBuffer.unlock();
    </code></pre></p>

    <p>Moving to render() function, draw triangle on screen!</p>

    <p><pre><code class="hx">
// Bind shader program we want to draw with
g.setProgram(program);
    
// Bind data we want to draw
g.setVertexBuffer(vertexBuffer);
g.setIndexBuffer(indexBuffer);

// Draw!
g.drawIndexedVertices();
    </code></pre></p>

    <p>For shaders, Kha uses GLSL syntax. Shaders are automatically converted based on target system using a tool called <a href="https://github.com/KTXSoftware/krafix">Krafix</a>, meaning we do not have to worry about rewriting them. The vertex shader.</p>

    <p><pre><code class="glsl">
// Input vertex data, different for all executions of this shader
attribute vec3 pos;

void kore() {
  // Just output position
  gl_Position = vec4(pos, 1.0);
}
    </code></pre></p>

    <p>The fragment shader.</p>

    <p><pre><code class="glsl">
void kore() {
  // Just output red color
  gl_FragColor = vec4(1.0, 0.0, 0.0, 1.0);
}
    </code></pre></p>

    <p>You can access complete sources <a href="https://github.com/luboslenco/kha3d_examples">here</a>.</p>

    <p>Build and run project as mentioned in tutorial 1, and you will be presented with <a href="http://luboslenco.com/kha3d/build/02">this</a>!</p>

    <p><img src="images/tutorial02.png" width="50%"/></p>
  </section>

  <section id="matrices">
    <h1>3.Matrices</h1>

    <p>(<a href="http://www.opengl-tutorial.org/beginners-tutorials/tutorial-3-matrices/">Reference</a>)</p>

    <p>This is where the reference tutorial comes in handy. As it points out, this is the most important part if you are just starting out with 3D programming.</p>

    <p><pre><code class="hx">
// Get a handle for our "MVP" uniform
mvpID = program.getConstantLocation("MVP");
    </code></pre></p>

    <p><pre><code class="hx">
// Projection matrix: 45Â° Field of View, 4:3 ratio, 0.1-100 display range
var projection = Matrix4.perspectiveProjection(45.0, 4.0 / 3.0, 0.1, 100.0);

// Camera matrix
var view = Matrix4.lookAt(new Vector3(4, 3, 3), // Position in World Space
              new Vector3(0, 0, 0), // and looks at the origin
              new Vector3(0, 1, 0) // Head is up
);

// Model matrix: an identity matrix (model will be at the origin)
var model = Matrix4.identity();
    </code></pre></p>

    <p><pre><code class="hx">
// Our ModelViewProjection: multiplication of our 3 matrices
// Remember, matrix multiplication is the other way around
mvp = Matrix4.identity();
mvp = mvp.multmat(projection);
mvp = mvp.multmat(view);
mvp = mvp.multmat(model);
    </code></pre></p>

    <p><pre><code class="hx">
// Send our transformation to the currently bound shader, in the "MVP" uniform
g.setMatrix(mvpID, mvp);
    </code></pre></p>

    <p><pre><code class="hx">
// Input vertex data, different for all executions of this shader
attribute vec3 pos;

// Values that stay constant for the whole mesh
uniform mat4 MVP;

void kore() {
  // Output position of the vertex, in clip space : MVP * position
  gl_Position = MVP * vec4(pos, 1.0);
}
    </code></pre></p>

    <p><pre><code class="json">
{
  "format": 2,
  "game": {
    "name": "Empty",
    "width": 640,
    "height": 480,
    "antiAliasingSamples": 2
  },
  "assets": [],
  "rooms": []
}
    </code></pre></p>


    <p>You can access complete sources <a href="https://github.com/luboslenco/kha3d_examples">here</a>.</p>

    <p><a href="http://luboslenco.com/kha3d/build/03">this</a></p>

    <p><img src="images/tutorial03.png" width="50%"/></p>
  </section>

  <section id="a-colored-cube">
    <h1>4.A colored cube</h1>

    <p>(<a href="http://www.opengl-tutorial.org/beginners-tutorials/tutorial-4-a-colored-cube/">Reference</a>)</p>

    <p><pre><code class="hx">
// An array of vertices to form a cube
static var vertices:Array&lt;Float&gt; = [
  -1.0,-1.0,-1.0,  -1.0,-1.0, 1.0,  -1.0, 1.0, 1.0,
   1.0, 1.0,-1.0,  -1.0,-1.0,-1.0,  -1.0, 1.0,-1.0,
   1.0,-1.0, 1.0,  -1.0,-1.0,-1.0,   1.0,-1.0,-1.0,
   1.0, 1.0,-1.0,   1.0,-1.0,-1.0,  -1.0,-1.0,-1.0,
  -1.0,-1.0,-1.0,  -1.0, 1.0, 1.0,  -1.0, 1.0,-1.0,
   1.0,-1.0, 1.0,  -1.0,-1.0, 1.0,  -1.0,-1.0,-1.0,
  -1.0, 1.0, 1.0,  -1.0,-1.0, 1.0,   1.0,-1.0, 1.0,
   1.0, 1.0, 1.0,   1.0,-1.0,-1.0,   1.0, 1.0,-1.0,
   1.0,-1.0,-1.0,   1.0, 1.0, 1.0,   1.0,-1.0, 1.0,
   1.0, 1.0, 1.0,   1.0, 1.0,-1.0,  -1.0, 1.0,-1.0,
   1.0, 1.0, 1.0,  -1.0, 1.0,-1.0,  -1.0, 1.0, 1.0,
   1.0, 1.0, 1.0,  -1.0, 1.0, 1.0,   1.0,-1.0, 1.0
];
// Array of colors for each cube vertex
static var colors:Array&lt;Float&gt; = [
  0.583, 0.771, 0.014,   0.609, 0.115, 0.436,   0.327, 0.483, 0.844,
  0.822, 0.569, 0.201,   0.435, 0.602, 0.223,   0.310, 0.747, 0.185,
  0.597, 0.770, 0.761,   0.559, 0.436, 0.730,   0.359, 0.583, 0.152,
  0.483, 0.596, 0.789,   0.559, 0.861, 0.639,   0.195, 0.548, 0.859,
  0.014, 0.184, 0.576,   0.771, 0.328, 0.970,   0.406, 0.615, 0.116,
  0.676, 0.977, 0.133,   0.971, 0.572, 0.833,   0.140, 0.616, 0.489,
  0.997, 0.513, 0.064,   0.945, 0.719, 0.592,   0.543, 0.021, 0.978,
  0.279, 0.317, 0.505,   0.167, 0.620, 0.077,   0.347, 0.857, 0.137,
  0.055, 0.953, 0.042,   0.714, 0.505, 0.345,   0.783, 0.290, 0.734,
  0.722, 0.645, 0.174,   0.302, 0.455, 0.848,   0.225, 0.587, 0.040,
  0.517, 0.713, 0.338,   0.053, 0.959, 0.120,   0.393, 0.621, 0.362,
  0.673, 0.211, 0.457,   0.820, 0.883, 0.371,   0.982, 0.099, 0.879
];
    </code></pre></p>

    <p><pre><code class="hx">
// Define vertex structure
var structure = new VertexStructure();
structure.add("pos", VertexData.Float3);
structure.add("col", VertexData.Float3);
// Save length - we store position and color data
var structureLength = 6;
    </code></pre></p>

    <p><pre><code class="hx">
// Copy vertices and colors to vertex buffer
var vbData = vertexBuffer.lock();
for (i in 0...Std.int(vbData.length / structureLength)) {
  vbData[i * structureLength] = vertices[i * 3];
  vbData[i * structureLength + 1] = vertices[i * 3 + 1];
  vbData[i * structureLength + 2] = vertices[i * 3 + 2];
  vbData[i * structureLength + 3] = colors[i * 3];
  vbData[i * structureLength + 4] = colors[i * 3 + 1];
  vbData[i * structureLength + 5] = colors[i * 3 + 2];
}
vertexBuffer.unlock();
    </code></pre></p>

    <p><pre><code class="hx">
// A 'trick' to create indices for a non-indexed vertex data
var indices:Array<Int> = [];
for (i in 0...Std.int(vertices.length / 3)) {
  indices.push(i);
}

// Create index buffer
indexBuffer = new IndexBuffer(
  indices.length, // Number of indices for our cube
  Usage.StaticUsage // Index data will stay the same
);

// Copy indices to index buffer
var iData = indexBuffer.lock();
for (i in 0...iData.length) {
  iData[i] = indices[i];
}
indexBuffer.unlock();
    </code></pre></p>

    <p><pre><code class="hx">
// Set depth mode
g.setDepthMode(true, CompareMode.Less);
    </code></pre></p>

    <p><pre><code class="glsl">
#ifdef GL_ES
precision highp float;
#endif

// Input vertex data, different for all executions of this shader
attribute vec3 pos;
attribute vec3 col;

// Output data - will be interpolated for each fragment.
varying vec3 fragmentColor;

// Values that stay constant for the whole mesh
uniform mat4 MVP;

void kore() {
  // Output position of the vertex, in clip space: MVP * position
  gl_Position = MVP * vec4(pos, 1.0);

  // The color of each vertex will be interpolated
  // to produce the color of each fragment
  fragmentColor = col;
}
    </code></pre></p>

    <p><pre><code class="glsl">
#ifdef GL_ES
precision mediump float;
#endif

// Interpolated values from the vertex shaders
varying vec3 fragmentColor;

void kore() {
  // Output color = color specified in the vertex shader,
  // interpolated between all 3 surrounding vertices
  gl_FragColor = vec4(fragmentColor, 1.0);
}
    </code></pre></p>


    <p>You can access complete sources <a href="https://github.com/luboslenco/kha3d_examples">here</a>.</p>

    <p><a href="http://luboslenco.com/kha3d/build/04">this</a></p>

    <p><img src="images/tutorial04.png" width="50%"/></p>
  </section>





  <section id="a-textured-cube">
    <h1>5.A textured cube</h1>

    <p>(<a href="http://www.opengl-tutorial.org/beginners-tutorials/tutorial-5-a-textured-cube/">Reference</a>)</p>

    <p><pre><code class="json">
{
  "format": 2,
  "game": {
    "name": "Empty",
    "width": 640,
    "height": 480,
    "antiAliasingSamples": 2
  },
  "assets": [
    {
      "file": "uvtemplate.png",
      "name": "uvtemplate",
      "type": "image"
    }
  ],
  "rooms": [
    {
      "assets": [
        "uvtemplate"
      ],
      "name": "room0",
      "neighbours": [],
      "parent": null
    }
  ]
}
    </code></pre></p>

    <p><pre><code class="hx">
// Array of texture coords for each cube vertex
static var uvs:Array&lt;Float&gt; = [
  0.000059, 0.000004,   0.000103, 0.336048,   0.335973, 0.335903, 
  1.000023, 0.000013,   0.667979, 0.335851,   0.999958, 0.336064, 
  0.667979, 0.335851,   0.336024, 0.671877,   0.667969, 0.671889, 
  1.000023, 0.000013,   0.668104, 0.000013,   0.667979, 0.335851, 
  0.000059, 0.000004,   0.335973, 0.335903,   0.336098, 0.000071, 
  0.667979, 0.335851,   0.335973, 0.335903,   0.336024, 0.671877, 
  1.000004, 0.671847,   0.999958, 0.336064,   0.667979, 0.335851, 
  0.668104, 0.000013,   0.335973, 0.335903,   0.667979, 0.335851, 
  0.335973, 0.335903,   0.668104, 0.000013,   0.336098, 0.000071, 
  0.000103, 0.336048,   0.000004, 0.671870,   0.336024, 0.671877, 
  0.000103, 0.336048,   0.336024, 0.671877,   0.335973, 0.335903, 
  0.667969, 0.671889,   1.000004, 0.671847,   0.667979, 0.335851
];
    </code></pre></p>

    <p><pre><code class="hx">
override public function init() {
  Configuration.setScreen(new LoadingScreen());

  // Load room with our texture
  Loader.the.loadRoom("room0", loadingFinished);
}
    </code></pre></p>

    <p><pre><code class="hx">
// Define vertex structure
var structure = new VertexStructure();
structure.add("pos", VertexData.Float3);
structure.add("uv", VertexData.Float2);
// Save length - we store position and uv data
var structureLength = 5;
    </code></pre></p>

    <p><pre><code class="hx">
// Get a handle for texture sample
textureID = program.getTextureUnit("myTextureSampler");

// Texture
image = Loader.the.getImage("uvtemplate");
    </code></pre></p>

    <p><pre><code class="hx">
// Copy vertices and uvs to vertex buffer
var vbData = vertexBuffer.lock();
for (i in 0...Std.int(vbData.length / structureLength)) {
  vbData[i * structureLength] = vertices[i * 3];
  vbData[i * structureLength + 1] = vertices[i * 3 + 1];
  vbData[i * structureLength + 2] = vertices[i * 3 + 2];
  vbData[i * structureLength + 3] = uvs[i * 2];
  vbData[i * structureLength + 4] = uvs[i * 2 + 1];
}
vertexBuffer.unlock();
    </code></pre></p>

    <p><pre><code class="hx">
Configuration.setScreen(this);
    </code></pre></p>

    <p><pre><code class="hx">
// Set texture
g.setTexture(textureID, image);
    </code></pre></p>

    <p><pre><code class="glsl">
#ifdef GL_ES
precision highp float;
#endif

// Input vertex data, different for all executions of this shader
attribute vec3 pos;
attribute vec2 uv;

// Output data: will be interpolated for each fragment.
varying vec2 vUV;

// Values that stay constant for the whole mesh
uniform mat4 MVP;

void kore() {
  // Output position of the vertex, in clip space: MVP * position
  gl_Position = MVP * vec4(pos, 1.0);

  // UV of the vertex. No special space for this one.
  vUV = uv;
}
    </code></pre></p>

    <p><pre><code class="glsl">
#ifdef GL_ES
precision mediump float;
#endif

// Interpolated values from the vertex shaders
varying vec2 vUV;

// Values that stay constant for the whole mesh.
uniform sampler2D myTextureSampler;

void kore() {

  // Output color = color of the texture at the specified UV
  gl_FragColor = texture2D(myTextureSampler, vUV);
}
    </code></pre></p>


    <p>You can access complete sources <a href="https://github.com/luboslenco/kha3d_examples">here</a>.</p>

    <p><a href="http://luboslenco.com/kha3d/build/05">this</a></p>

    <p><img src="images/tutorial05.png" width="50%"/></p>
  </section>




  <section id="keyboard-and-mouse">
    <h1>6.Keyboard and mouse</h1>

    <p>(<a href="http://www.opengl-tutorial.org/beginners-tutorials/tutorial-6-keyboard-and-mouse/">Reference</a>)</p>

    <p><pre><code class="hx">
// Add mouse and keyboard listeners
Mouse.get().notify(onMouseDown, onMouseUp, onMouseMove, null);
Keyboard.get().notify(onKeyDown, onKeyUp);

// Used to calculate delta time
lastTime = Scheduler.time();
    </code></pre></p> 

    <p><pre><code class="hx">
function onMouseDown(button:Int, x:Int, y:Int) {
  isMouseDown = true;
}

function onMouseUp(button:Int, x:Int, y:Int) {
  isMouseDown = false;
}

function onMouseMove(x:Int, y:Int) {
  mouseDeltaX = x - mouseX;
  mouseDeltaY = y - mouseY;

  mouseX = x;
  mouseY = y;
}

function onKeyDown(key:Key, char:String) {
  if (key == Key.UP) moveForward = true;
  else if (key == Key.DOWN) moveBackward = true;
  else if (key == Key.LEFT) strafeLeft = true;
  else if (key == Key.RIGHT) strafeRight = true;
}

function onKeyUp(key:Key, char:String) {
  if (key == Key.UP) moveForward = false;
  else if (key == Key.DOWN) moveBackward = false;
  else if (key == Key.LEFT) strafeLeft = false;
  else if (key == Key.RIGHT) strafeRight = false;
}
    </code></pre></p>

    <p><pre><code class="hx">
override public function update() {
  // Compute time difference between current and last frame
  var deltaTime = Scheduler.time() - lastTime;
  lastTime = Scheduler.time();

  // Compute new orientation
  if (isMouseDown) {
    horizontalAngle += mouseSpeed * mouseDeltaX * -1;
    verticalAngle += mouseSpeed * mouseDeltaY * -1;
  }

  // Direction : Spherical coordinates to Cartesian coordinates conversion
  var direction = new Vector3(
    Math.cos(verticalAngle) * Math.sin(horizontalAngle),
    Math.sin(verticalAngle),
    Math.cos(verticalAngle) * Math.cos(horizontalAngle)
  );
  
  // Right vector
  var right = new Vector3(
    Math.sin(horizontalAngle - 3.14 / 2.0), 
    0,
    Math.cos(horizontalAngle - 3.14 / 2.0)
  );
  
  // Up vector
  var up = right.cross(direction);

  // Movement
  if (moveForward) {
    var v = direction.mult(deltaTime * speed);
    position = position.add(v);
  }
  if (moveBackward) {
    var v = direction.mult(deltaTime * speed * -1);
    position = position.add(v);
  }
  if (strafeRight) {
    var v = right.mult(deltaTime * speed);
    position = position.add(v);
  }
  if (strafeLeft) {
    var v = right.mult(deltaTime * speed * -1);
    position = position.add(v);
  }

  // Look vector
  var look = position.add(direction);
  
  // Camera matrix
  view = Matrix4.lookAt(position, // Camera is here
              look, // and looks here : at the same position, plus "direction"
              up // Head is up (set to (0, -1, 0) to look upside-down)
  );
  
  // Update model-view-projection matrix
  mvp = Matrix4.identity();
  mvp = mvp.multmat(projection);
  mvp = mvp.multmat(view);
  mvp = mvp.multmat(model);

  mouseDeltaX = 0;
  mouseDeltaY = 0;
}
    </code></pre></p>

    <p><pre><code class="hx">
// Set culling
g.setCullMode(CullMode.CounterClockwise);
    </code></pre></p>

    
    <p>You can access complete sources <a href="https://github.com/luboslenco/kha3d_examples">here</a>.</p>

    <p><a href="http://luboslenco.com/kha3d/build/06">this</a></p>

    <p><img src="images/tutorial06.png" width="50%"/></p>
  </section>





  <section id="model-loading">
    <h1>7.Model loading</h1>

    <p>(<a href="http://www.opengl-tutorial.org/beginners-tutorials/tutorial-7-model-loading/">Reference</a>)</p>

    <p><pre><code class="json">
{
  "format": 2,
  "game": {
    "name": "Empty",
    "width": 640,
    "height": 480,
    "antiAliasingSamples": 2
  },
  "assets": [
    {
      "file": "uvmap.png",
      "name": "uvmap",
      "type": "image"
    },
    {
      "file": "cube.obj",
      "name": "cube",
      "type": "blob"
    }
  ],
  "rooms": [
    {
      "assets": [
        "uvmap",
        "cube"
      ],
      "name": "room0",
      "neighbours": [],
      "parent": null
    }
  ]
}
    </code></pre></p>

    <p><pre><code class="hx">
// Define vertex structure
var structure = new VertexStructure();
structure.add("pos", VertexData.Float3);
structure.add("uv", VertexData.Float2);
structure.add("nor", VertexData.Float3);
// Save length - we store position, uv and normal data
var structureLength = 8;
    </code></pre></p>

    <p><pre><code class="hx">
// Parse .obj file
var obj = new ObjLoader(Loader.the.getBlob("cube").toString());
var data = obj.data;
var indices = obj.indices;

// Create vertex buffer
vertexBuffer = new VertexBuffer(
  Std.int(data.length / structureLength), // Vertex count
  structure, // Vertex structure
  Usage.StaticUsage // Vertex data will stay the same
);

// Copy data to vertex buffer
var vbData = vertexBuffer.lock();
for (i in 0...vbData.length) {
  vbData[i] = data[i];
}
vertexBuffer.unlock();

// Create index buffer
indexBuffer = new IndexBuffer(
  indices.length, // Number of indices for our cube
  Usage.StaticUsage // Index data will stay the same
);

// Copy indices to index buffer
var iData = indexBuffer.lock();
for (i in 0...iData.length) {
  iData[i] = indices[i];
}
indexBuffer.unlock();
    </code></pre></p>

    <p>You can access complete sources <a href="https://github.com/luboslenco/kha3d_examples">here</a>.</p>

    <p><a href="http://luboslenco.com/kha3d/build/07">this</a></p>

    <p><img src="images/tutorial07.png" width="50%"/></p>
  </section>

  <section id="basic-shading">
    <h1>8.Basic shading</h1>

    <p><pre><code class="hx">
viewMatrixID = program.getConstantLocation("V");
modelMatrixID = program.getConstantLocation("M");
lightID = program.getConstantLocation("lightPos");
    </code></pre></p>

    <p><pre><code class="hx">
var obj = new ObjLoader(Loader.the.getBlob("suzanne").toString());
    </code></pre></p>

    <p><pre><code class="hx">
g.setMatrix(modelMatrixID, model);
g.setMatrix(viewMatrixID, view);

// Set light position to (4, 4, 4)
g.setFloat3(lightID, 4, 4, 4);
    </code></pre></p>

    <p><pre><code class="glsl">
#ifdef GL_ES
precision highp float;
#endif

// Input vertex data, different for all executions of this shader
attribute vec3 pos;
attribute vec2 uv;
attribute vec3 nor;

// Output data: will be interpolated for each fragment
varying vec2 vUV;
varying vec3 positionWorldspace;
varying vec3 normalCameraspace;
varying vec3 eyeDirectionCameraspace;
varying vec3 lightDirectionCameraspace;

// Values that stay constant for the whole mesh
uniform mat4 MVP;
uniform mat4 V;
uniform mat4 M;
uniform vec3 lightPos;

void kore() {
  // Output position of the vertex, in clip space: MVP * position
  gl_Position = MVP * vec4(pos, 1.0);
  
  // Position of the vertex, in worldspace : M * position
  positionWorldspace = (M * vec4(pos, 1.0)).xyz;
  
  // Vector that goes from the vertex to the camera, in camera space.
  // In camera space, the camera is at the origin (0,0,0).
  vec3 vertexPositionCameraspace = (V * M * vec4(pos, 1.0)).xyz;
  eyeDirectionCameraspace = vec3(0.0, 0.0, 0.0) - vertexPositionCameraspace;

  // Vector that goes from the vertex to the light, in camera space. M is ommited because it's identity.
  vec3 lightPositionCameraspace = (V * vec4(lightPos, 1.0)).xyz;
  lightDirectionCameraspace = lightPositionCameraspace + eyeDirectionCameraspace;
  
  // Normal of the the vertex, in camera space
  normalCameraspace = (V * M * vec4(nor, 0.0)).xyz; // Only correct if modelMatrix does not scale the model! Use its inverse transpose if not.
  
  // UV of the vertex. No special space for this one.
  vUV = uv;
}
    </code></pre></p>

    <p><pre><code class="glsl">
#ifdef GL_ES
precision mediump float;
#endif

// Interpolated values from the vertex shaders
varying vec2 vUV;
varying vec3 positionWorldspace;
varying vec3 normalCameraspace;
varying vec3 eyeDirectionCameraspace;
varying vec3 lightDirectionCameraspace;

// Values that stay constant for the whole mesh.
uniform sampler2D myTextureSampler;
uniform vec3 lightPos;

void kore() {

  // Light emission properties
  // You probably want to put them as uniforms
  vec3 lightColor = vec3(1.0,1.0,1.0);
  float lightPower = 50.0;
  
  // Material properties
  vec3 materialDiffuseColor = texture2D(myTextureSampler, vUV).rgb;
  vec3 materialAmbientColor = vec3(0.1, 0.1, 0.1) * materialDiffuseColor;
  vec3 materialSpecularColor = vec3(0.3, 0.3, 0.3);

  // Distance to the light
  float distance = length(lightPos - positionWorldspace);

  // Normal of the computed fragment, in camera space
  vec3 n = normalize(normalCameraspace);
  // Direction of the light (from the fragment to the light)
  vec3 l = normalize(lightDirectionCameraspace);
  // Cosine of the angle between the normal and the light direction, 
  // clamped above 0
  //  - light is at the vertical of the triangle -> 1
  //  - light is perpendicular to the triangle -> 0
  //  - light is behind the triangle -> 0
  float cosTheta = clamp(dot(n, l), 0.0, 1.0);
  
  // Eye vector (towards the camera)
  vec3 E = normalize(eyeDirectionCameraspace);
  // Direction in which the triangle reflects the light
  vec3 R = (-l) - 2.0 * dot(n, (-l)) * n;
  //vec3 R = reflect(-l,n); // TODO: waiting for krafix fix

  // Cosine of the angle between the Eye vector and the Reflect vector,
  // clamped to 0
  //  - Looking into the reflection - 1
  //  - Looking elsewhere - &lt; 1
  float cosAlpha = clamp(dot(E, R), 0.0, 1.0);
  
  gl_FragColor = vec4(vec3(
    // Ambient: simulates indirect lighting
    materialAmbientColor +
    // Diffuse: "color" of the object
    materialDiffuseColor * lightColor * lightPower * cosTheta / (distance * distance) +
    // Specular: reflective highlight, like a mirror
    materialSpecularColor * lightColor * lightPower * pow(cosAlpha, 5.0) / (distance * distance)
  ), 1.0);
}
    </code></pre></p>

    <p>(<a href="http://www.opengl-tutorial.org/beginners-tutorials/tutorial-8-basic-shading/">Reference</a>)</p>

    
    <p>You can access complete sources <a href="https://github.com/luboslenco/kha3d_examples">here</a>.</p>
    
    <p><a href="http://luboslenco.com/kha3d/build/08">this</a></p>

    <p><img src="images/tutorial08.png" width="50%"/></p>
  </section>

	<footer><a href="http://twitter.com/luboslenco">@luboslenco</a> | <a href="https://github.com/luboslenco/kha3d_examples">Contribute</a></footer>
  </article>

  <script type="text/javascript" src="js/jquery-1.11.0.min.js"></script>
  <script type="text/javascript" src="js/index.js"></script>
</body>
</html>
